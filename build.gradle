
plugins {
    id 'java-library'
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.google.protobuf' version '0.8.10'
    id 'osgi'
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version '0.21.0'
    id "de.marcphilipp.nexus-publish" version "0.3.0"
}

// Update version here, repeated check-ins not into master will have snapshot on them
def versionMajor = 2
def versionMinor = 2
def versionPatch = 2
def versionModifier = ""
def jarVersion = "2.2.2"
def branch = System.getenv("TRAVIS_BRANCH");
def tag = System.getenv("TRAVIS_TAG");
def useSigning = "master".equals(branch) || (!"".equals(tag) && tag.equals(branch)) // tag will be the branch on a tag event for travis

def getVersionName = { ->
    if ("".equals(tag))  {
        versionModifier = "-SNAPSHOT"
    }

    if (versionModifier != null && versionModifier.length() > 0) {
        return "" + versionMajor + "." + versionMinor + "." + versionPatch + versionModifier
    } else {
        return "" + versionMajor + "." + versionMinor + "." + versionPatch
    }
}

version = getVersionName()
archivesBaseName = 'java-nats-streaming'
group = 'io.nats'

// Get signing properties set up from outside (env var can't handle the dot)
if (System.getenv('SONATYPE_USERNAME') != null) {
    project.ext['ossrhUsername'] = System.getenv('SONATYPE_USERNAME')
    project.ext['ossrhPassword'] = System.getenv('SONATYPE_PASSWORD')
    project.ext['signing.secretKeyRingFile'] = System.getenv('GPG_KEYRING_FILE')
    project.ext['signing.keyId'] = System.getenv('GPG_KEY_ID')
    project.ext['signing.password'] =  System.getenv('GPG_KEY_PASSPHRASE')
}

// If these aren't set, just set them to empty so we don't have issues getting them
if (!project.hasProperty('ossrhUsername')) {
    project.ext['ossrhUsername'] = ""
    project.ext['ossrhPassword'] = ""
}

tasks {
    closeRepository {
        onlyIf { nexusPublishing.useStaging.get() }
    }
    releaseRepository{
        onlyIf { nexusPublishing.useStaging.get() }
    }
}

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/releases"
    }
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.9.1'

    testImplementation 'junit:junit:4.12'

    implementation 'io.nats:jnats:2.6.5'
}

sourceSets {
    generated {
        java.srcDirs = ['gen/main/java']
    }
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDirs = ['src/main/java', 'src/examples/java', 'gen/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

osgiClasses {
    exclude("io/nats/examples/**")
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.9+'
    }

    generateProtoTasks.generatedFilesBaseDir = 'gen'

    generateProtoTasks {
        all().each { task ->
        }
        ofSourceSet('generated')
    }   
}

compileGeneratedJava {
    options.warnings = false
}

clean {
    delete protobuf.generatedFilesBaseDir
}

jar {
    manifest {
        attributes('Implementation-Title': 'Java Nats Streaming',
                   'Implementation-Version': jarVersion,
                   'Implementation-Vendor': 'nats.io')
    }
    exclude("io/nats/streaming/examples/**")
}

test {
    maxHeapSize = "2g"
    if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        jvmArgs '-Djava.security.egd=file:/dev/./urandom'
    }
    testLogging {
        exceptionFormat = 'full'
        events "started", "passed", "skipped", "failed"
    }
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "NATS.IO Java Streaming API"
    excludes = ['io/nats/streaming/examples','io/nats/streaming/protobuf']
    classpath = sourceSets.main.runtimeClasspath
    doLast {
            exec {
                println "Updating favicon on all html files"
                workingDir 'build/docs/javadoc'
                // Only on linux, mac at this point
                commandLine 'find', '.', '-name', '*.html', '-exec', 'sed', '-i', '-e', 's#<head>#<head><link rel="icon" type="image/ico" href="favicon.ico">#', '{}', ';'
            }
            copy {
                println "Copying images to javadoc folder"
                from 'src/main/javadoc/images'
                into 'build/docs/javadoc'
            }
        }
}

task examplesJar(type: Jar) {
    classifier = 'examples'
    manifest {
        attributes('Implementation-Title': 'Java Nats Streaming Examples',
                   'Implementation-Version': jarVersion,
                   'Implementation-Vendor': 'nats.io')
    }
    from(sourceSets.main.output) {
        include "io/nats/streaming/examples/**"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    afterEvaluate { // only report on main library not examples
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/examples**', '**/protobuf**'])
        })
    }
}

artifacts {
    archives javadocJar, sourcesJar, examplesJar
}

if (useSigning) {
    signing {
        sign configurations.archives
    }
}

nexusStaging {
    packageGroup = group
    username = project.getProperty('ossrhUsername')
    password = project.getProperty('ossrhPassword')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact examplesJar
            artifact javadocJar
            pom {
                name = 'jstan'
                packaging = 'jar'
                groupId = group
                artifactId = archivesBaseName
                description = 'Client library for working with the NATS streaming system.'
                url = 'https://github.com/nats-io/stan.java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "synadia"
                        name = "Synadia"
                        email = "info@synadia.com"
                        url = "https://nats.io"
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/stan.java'
                }
            }

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)

                if (useSigning) {
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }
            }

            // create the signed artifacts
            if (useSigning) {
                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc|examples)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
    }

    nexusPublishing {
        repositories {
            sonatype {
                username = project.getProperty('ossrhUsername')
                password = project.getProperty('ossrhPassword')
            }
        }
    }

    model {
        tasks.generatePomFileForMavenJavaPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        
        tasks.publishMavenJavaPublicationToSonatypeRepository {
            dependsOn project.tasks.signArchives
        }
    }
}
